AC_INIT(popt.h)
AM_CONFIG_HEADER(config.h)
AC_PREREQ(2.12)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(popt, 1.5)
ALL_LINGUAS="ro sk"

AC_ISC_POSIX

AC_PROG_CC
AC_GCC_TRADITIONAL
AM_C_PROTOTYPES

dnl AM_DISABLE_SHARED
AM_PROG_LIBTOOL

AC_PROG_INSTALL

if test "X$CC" = Xgcc; then
    CFLAGS="-Wall $CFLAGS"
fi
addlib() {
  l=$1
  shift
  case "$target" in 
    *-*-solaris*)	LIBS="$LIBS -L$l -R $l $*";;
    *)		LIBS="$LIBS -L$l $*";;
  esac
}

dnl
dnl if CC is gcc, we can rebuild the dependencies (since the depend rule
dnl requires gcc).  If it's not, don't rebuild dependencies -- use what was
dnl shipped with RPM.
dnl
if test X"$GCC" = "Xyes" ; then
    TARGET="depend allprogs"
else
    TARGET="everything"
    #
    # let the Makefile know that we're done with `depend', since we don't
    # have gcc we're not going to rebuild our dependencies at all.
    #
    echo > .depend-done
fi
AC_SUBST(TARGET)

AC_CHECK_HEADERS(alloca.h libintl.h mcheck.h unistd.h)
AC_MSG_CHECKING(for /usr/ucblib in LIBS)
if test -d /usr/ucblib ; then
	if test "$build" = "mips-sni-sysv4" ; then
		addlib /usr/ccs/lib -lc
	fi

	addlib /usr/ucblib
	
	AC_MSG_RESULT(yes)
else
	AC_MSG_RESULT(no)
fi

if test ! -f ../rpm.c
then
  AC_MSG_CHECKING(for GNU xgettext)
  xgettext --version 2>&1 | grep 'GNU gettext' >/dev/null 2>&1 || AC_MSG_ERROR([
  *** GNU gettext is required. The latest version
  *** is always available from ftp://ftp.gnu.org/gnu/gettext/.])
  AC_MSG_RESULT(yes)
fi

AC_CHECK_FUNCS(strerror mtrace)
dnl AC_CHECK_FUNCS(gettext)
dnl AC_CHECK_FUNCS(dgettext)
AC_CHECK_FUNC(setreuid, [], [
    AC_CHECK_LIB(ucb, setreuid, [if echo $LIBS | grep -- -lucb >/dev/null ;then :; else LIBS="$LIBS -lc -lucb" USEUCB=y;fi])
])

AM_GNU_GETTEXT

POPT_SOURCE_PATH="`pwd`"
AC_DEFINE_UNQUOTED(POPT_SOURCE_PATH, "$POPT_SOURCE_PATH")
AC_SUBST(POPT_SOURCE_PATH)

AC_OUTPUT([Doxyfile Makefile intl/Makefile po/Makefile.in],
	[sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])
