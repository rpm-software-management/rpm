AT_TESTED([rpm rpmbuild rpmquery])

m4_define([RPMDB_INIT],[[
runroot rpm --initdb
]])

m4_define([RPMDB_CLEAR],[[
rm -rf "${abs_builddir}"/testing`rpm --eval '%_dbpath'`/*
]])

m4_define([RPMPY_RUN],[[
cat << EOF > test.py
# coding=utf-8
import rpm, sys
dbpath=rpm.expandMacro('%_dbpath')
rpm.addMacro('_dbpath', '${abs_builddir}/testing%s' % dbpath)
def myprint(msg = ''):
    sys.stdout.write('%s\n' % msg)
$1
EOF
${PYTHON} test.py
]])

m4_define([RPMPY_CHECK],[
AT_SKIP_IF([$PYTHON_DISABLED])
AT_CHECK([RPMPY_RUN([$1])], [], [$2], [$3])
])

m4_define([RPMPY_TEST],[
AT_SETUP([$1])
AT_KEYWORDS([python])
RPMDB_CLEAR
RPMDB_INIT
RPMPY_CHECK([$2], [$3], [$4])
AT_CLEANUP
])

# Enable colored test output if available
m4_ifdef([AT_COLOR_TESTS], [AT_COLOR_TESTS])

AT_INIT
